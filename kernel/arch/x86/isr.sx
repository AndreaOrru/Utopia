#include <layout.h>

.intel_syntax noprefix

#define EXCEPTION(x)     (x <  32)
#define NO_ERROR(x)      (x !=  8  &&  !(x >= 10 && x <= 14) && x != 17)
#define IRQ(x)           (x >= 32  &&  x < 32 + 16)
#define IRQ_SLAVE(x)     (x -  32  >=  8)

.text
.macro isr_generate n
    .align 4
    .global isr\n
    .type isr\n, @function
    isr\n:
        .if NO_ERROR(\n)
            push 0
        .endif
        push \n
        pusha

        mov bp, 0x10
        mov ds, bp
        mov es, bp

        .if EXCEPTION(\n)
            mov [context], esp
        .endif
        mov esp, INTERRUPT_STACK - 4
        call [interrupt_handlers + (\n * 4)]
        mov esp, [context]

        .if IRQ(\n)
            mov al, 0x20
            .if IRQ_SLAVE(\n)
                out 0xA0, al
            .endif
            out 0x20, al
        .endif

        mov bp, 0x23
        mov ds, bp
        mov es, bp

        popa
        add esp, 8
        iret
.endm

isr_generate 0
isr_generate 1
isr_generate 2
isr_generate 3
isr_generate 4
isr_generate 5
isr_generate 6
isr_generate 7
isr_generate 8
isr_generate 9
isr_generate 10
isr_generate 11
isr_generate 12
isr_generate 13
isr_generate 14
isr_generate 15
isr_generate 16
isr_generate 17
isr_generate 18
isr_generate 19
isr_generate 20
isr_generate 21
isr_generate 22
isr_generate 23
isr_generate 24
isr_generate 25
isr_generate 26
isr_generate 27
isr_generate 28
isr_generate 29
isr_generate 30
isr_generate 31

isr_generate 32
isr_generate 33
isr_generate 34
isr_generate 35
isr_generate 36
isr_generate 37
isr_generate 38
isr_generate 39
isr_generate 40
isr_generate 41
isr_generate 42
isr_generate 43
isr_generate 44
isr_generate 45
isr_generate 46
isr_generate 47
